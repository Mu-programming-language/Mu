Helpers
	lower_case = ['a' .. 'z'];
	upper_case = ['A' .. 'Z'];
	letter = lower_case | upper_case;
	digit = ['0' .. '9'];
	space = ' ';
	tab = 9;
	lf = 10;
	cr = 13;
	all = [0 .. 0xFFFF];
	eol = cr lf | cr | lf;
	blank = (space | tab | eol);
	all_except_eol = [all - [cr + lf]];
	not_star =    [all - '*'] | eol;
    not_star_not_slash = [all - ['*' + '/']] | eol;
	all_except_double_quote = [all - '"'];

Tokens
	analysis = 'analysis';
	algebra = 'algebra';
	set_theory = 'set theory';
	geometry = 'geometry';
	statistic_and_probability = 'statistic and probability';

	category = 'category';
	proof = 'proof';
	statement = 'statement';
	ask = 'ask';

	let = 'let';
	done = 'done';
	show = 'show';
	thus = 'thus';

	deprived_of = '\';

	not = '¬' /*| 'not'*/;

	complex = 'C';
	real = 'R';
	rational = 'Q';
	integer = 'Z';
	natural = 'N';
	empty_set = '∅';

	multiplication = '*';
	division = '/';
	modulo = '%';
	plus = '+';
	minus = '-';
	power = '^';

	for_all = '∀' /*| 'forall'*/;
	exist = '∃' /*| 'exists'*/;

	such_as = '|' /*| 'suchas'*/;
	comma = ',';
	semicolon = ';';
	column = ':';
	hashtag = '#';
	implication_sign = '=>';
	equivalence_sign = '<=>';

	logical_and = '∧' /*| 'and'*/;
	logical_or = '∨' /*| 'or'*/;

	union = '∪' /*| 'union'*/;
	intersection = '∩' /*| 'intersection'*/;

	subset_of = '⊂' /*| 'subsetof'*/;
	superset_of = '⊃' /*| 'supersetof'*/;
	not_a_subset_of = '⊄';
	not_a_superset_of = '⊅';

	element_of = '∈' /*| 'elementof'*/;
	contains_as_member = '∋' /*| 'containsasmember'*/;
	not_an_element_of = '∉';
	does_not_contain_as_member = '∌';

	left_brace = '{';
	right_brace = '}';
	left_parenthesis = '(';
	right_parenthesis = ')';
	lelft_bracket = '[';
	right_bracket = ']';

	equal_to = '=';
	greater_than = '>';
	less_than = '<';
	greater_than_or_equal_to = '≥' | '>=';
	less_than_or_equal_to = '≤' | '<=';
	not_equal_to = '≠' | '!=';
	not_greater_than = '≯' | '!>';
	not_less_than = '≮' | '!<';
	neither_greater_than_nor_equal_to = '≱' | '!>=';
	neither_less_than_nor_equal_to = '≰' | '!<=';

	number = '0' | (['1'..'9'] digit*);

	set_identifier = upper_case;
	element_identifier = lower_case;

	string = '"' all_except_double_quote* '"';

	blank = blank+;
	short_comment = '//' all_except_eol* eol?;
	long_comment = '/*' not_star+ '*'+ (not_star_not_slash not_star* '*'+)* '/';

Ignored Tokens
	blank,
	short_comment,
	long_comment;

Productions
	program =
		{analysis} category analysis semicolon |
		{algebra} category algebra semicolon |
		{set_theory} category set_theory semicolon instructions |
		{geometry} category geometry semicolon |
		{statistic_and_probability} category statistic_and_probability semicolon;
	instructions =
		statement_block ask_block proof_block;
	statement_block =
		statement column statement_instruction_list;
	ask_block =
		ask column ask_instruction_list;
	proof_block =
		proof column proof_instruction_list;
	statement_instruction_list =
		statement_instruction additional_statement_instruction* semicolon;
	additional_statement_instruction =
		comma statement_instruction;
	statement_instruction =
		{declaration} let set_identifier |
		{assignment} [new]:set_identifier equal_to left_brace element_identifier element_of set_term such_as element_expression right_brace |
		{definition} let [new]:set_identifier equal_to left_brace element_identifier element_of set_term such_as element_expression right_brace;
	ask_instruction_list =
		ask_instruction additional_ask_instruction* semicolon;
	additional_ask_instruction =
		comma ask_instruction;
	ask_instruction =
		hashtag number column show set_expression;
	proof_instruction_list =
		proof_instruction additional_proof_instruction*;
	additional_proof_instruction =
		proof_instruction;
	proof_instruction =
		hashtag number column demonstration done semicolon;

	demonstration =
		proposition_list+;
	proposition_list =
		{proposition} proposition additional_proposition* semicolon |
		{conclusion} thus expression semicolon;
	additional_proposition =
		{implication} implication_sign proposition |
		{equivalence} equivalence_sign proposition;
	proposition =
		{expression} expression |
		{for_all} for_all element_identifier element_of set_expression comma expression |
		{exist} exist element_identifier element_of set_expression comma expression;

	expression =
		and_expression;
	and_expression =
		{logical_or} or_expression |
		{logical_and} and_expression logical_and or_expression;
	or_expression =
		{expression} simple_expression |
		{logical_or} or_expression logical_or simple_expression;
	simple_expression =
		{expression} element_expression element_of set_expression |
		{set_expression} set_expression |
		{element_expression} element_expression;

	set_expression =
		{comparison} set_comparison;
	set_comparison =
		{arithmetic} set_arithmetic |
		{equal_to} set_comparison equal_to set_arithmetic |
		{not_equal_to} set_comparison not_equal_to set_arithmetic |
		{subset_of} set_comparison subset_of set_arithmetic |
		{not_a_subset_of} set_comparison not_a_subset_of set_arithmetic |
		{superset_of} set_comparison superset_of set_arithmetic |
		{not_a_superset_of} set_comparison not_a_superset_of set_arithmetic;
	set_arithmetic =
		{factor} set_factor |
		{plus} set_arithmetic plus set_factor |
		{deprived_of} set_arithmetic deprived_of set_factor |
		{union} set_arithmetic union set_factor |
		{intersection} set_arithmetic intersection set_factor;
	set_factor =
		{term} set_exponent |
		{mult} set_factor multiplication set_exponent;
	set_exponent =
		{term} set_term |
		{exp} set_exponent power set_term;
	set_term =
		{complex} complex |
		{real} real |
		{rational} rational |
		{integer} integer |
		{natural} natural |
		{empty} empty_set |
		{identifier} set_identifier |
		{element} left_brace element_term right_brace |
		{expression} left_parenthesis set_expression right_parenthesis;

	element_expression =
		{comparison} element_comparison;
	element_comparison =
		{arithmetic} element_arithmetic |
		{equal_to} element_comparison equal_to element_arithmetic |
		{not_equal_to} element_comparison not_equal_to element_arithmetic |
		{greater_than} element_comparison greater_than element_arithmetic |
		{not_greater_than} element_comparison not_greater_than element_arithmetic |
		{less_than} element_comparison less_than element_arithmetic |
		{not_less_than} element_comparison not_less_than element_arithmetic |
		{greater_than_or_equal_to} element_comparison greater_than_or_equal_to element_arithmetic |
		{neither_greater_than_nor_equal_to} element_comparison neither_greater_than_nor_equal_to element_arithmetic |
		{less_than_or_equal_to} element_comparison less_than_or_equal_to element_arithmetic |
		{neither_less_than_nor_equal_to} element_comparison neither_less_than_nor_equal_to element_arithmetic;
	element_arithmetic =
		{factor} element_factor |
		{plus} element_arithmetic plus element_factor |
		{minus} element_arithmetic minus element_factor;
	element_factor =
		{term} element_term |
		{multiplication} element_factor multiplication element_term |
		{division} element_factor division element_term |
		{modulo} element_factor modulo element_term;
	element_term =
		{number} number |
		{element} element_identifier |
		{expression} left_parenthesis element_expression right_parenthesis;
